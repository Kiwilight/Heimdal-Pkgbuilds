# $Id: PKGBUILD 66461 2012-02-25 03:07:56Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs
pkgver=5.0.6
pkgrel=3
pkgdesc='A kernel-based automounter for Linux.'
arch=('i686' 'x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libldap' 'libxml2' 'heimdal')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/conf.d/autofs')
options=(!makeflags)
install='autofs.install'
source=("http://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar.bz2"
        'heimdal.diff'
        "http://www.kernel.org/pub/linux/daemons/${pkgname}/v5/patches-5.0.7/${pkgname}-${pkgver}-fix-ipv6-name-for-lookup-fix.patch"
        'autofs'
        'autofs.conf.d'
        'auto.master'
        'auto.misc')
md5sums=('44cea7f711e2290a0b50fddeda079705'
         'bb87df48e42f2d3f84364ca45d8bfc53'
         '221bf50d6e496b22014a9a7b234fe556'
         'e307bf6d2638e46eeb916cf42fe029b2'
         '47f597c870410055e0fdb66103daf928'
         'a6cefb591e77b31b79dbb7243646c96b'
         'd8a15ec9186c5c0b36e5cea1e2739e8a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "$srcdir/heimdal.diff"

  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" \
    Makefile.rules

  patch -p1 -i "../${pkgname}-${pkgver}-fix-ipv6-name-for-lookup-fix.patch"

  ./configure --prefix=/usr --sysconfdir=/etc/autofs --with-mapdir=/etc/autofs --without-hesiod \
    --enable-ignore-busy
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make INSTALLROOT="${pkgdir}" install

  install -Dm0644 "${srcdir}/auto.master" "${pkgdir}/etc/autofs/auto.master"
  install -Dm0644 "${srcdir}/auto.misc" "${pkgdir}/etc/autofs/auto.misc"
  install -Dm0755 "${srcdir}/autofs" "${pkgdir}/etc/rc.d/autofs"
  install -Dm0644 "${srcdir}/autofs.conf.d" "${pkgdir}/etc/conf.d/autofs"
}
