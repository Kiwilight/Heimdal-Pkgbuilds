# $Id: PKGBUILD 70173 2012-05-01 11:40:01Z andrea $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>

pkgname=freeradius
pkgver=2.1.12
pkgrel=5
pkgdesc="The premier open source RADIUS server"
arch=('i686' 'x86_64')
url="http://www.freeradius.org/"
license=('GPL')
depends=('heimdal' 'pth' 'net-snmp' 'postgresql-libs' 'libmysqlclient')
makedepends=('libpcap' 'unixodbc' 'python2')
optdepends=('libpcap' 'unixodbc' 'python2')
options=('!libtool' '!makeflags')
install=$pkgname.install
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"{,.sig}
	radiusd)
md5sums=('862d3a2c11011e61890ba84fa636ed8c'
         '8759569d36b9f353c281e24875b556be'
         'f1a6530b1b69d2fa793aa45b2de379bb')

build() {
  cd $srcdir/freeradius-server-$pkgver
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  ./configure --with-system-libtool --with-system-libltdl \
	      --prefix=/usr --enable-heimdal-krb5 \
	      --localstatedir=/var \
	      --sysconfdir=/etc \
	      --libdir=/usr/lib/freeradius
  make
}

package() {
  cd $srcdir/freeradius-server-$pkgver
  make install R=$pkgdir
  install -D -m755 ../radiusd $pkgdir/etc/rc.d/radiusd
  chmod o+r $pkgdir/etc/raddb/*
  mv $pkgdir/etc/raddb $pkgdir/etc/raddb.default
  rm -rf $pkgdir/var/run
}
