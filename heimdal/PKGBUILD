# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgdesc='A Swedish implementation of the Kerberos v5 SSO architecture'
url='http://www.h5l.org/'

pkgname='heimdal'
pkgver='1.5.1'
pkgrel='1'
arch=('i686' 'x86_64')
license=('custom')
depends=('db' 'e2fsprogs' 'libldap' 'openssl' 'sqlite3')
backup=('etc/krb5.conf')
options=('!libtool' '!emptydirs')

install=heimdal.install

source=("http://www.h5l.org/dist/src/$pkgname-$pkgver.tar.gz"
  kdc.rc.d kadmind.conf.d kpasswd.conf.d kadmind.rc.d kpasswd.rc.d)
md5sums=('0709951e91560f788b4b7cb125604b2b'
         '5312a21dc5ed87c5ad58c819697f8458'
         'f29dea6d3d6eb4bbb6a0704c07434e3f'
         'edb2b1bbf51b2450d9d26e88b8946086'
         '74d4ad624370d8f35000224d68cdcc95'
         '92f79646bedab127136b341069bbdeb1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # kcm is broken right now

  ./configure --prefix=/usr --libexecdir=/usr/sbin \
    --sysconfdir=/etc --mandir=/usr/share/man \
    --with-berkeley-db --with-openldap=/usr \
    --with-openssl=/usr --with-readline=/usr \
    --with-sqlite3=/usr
  unset MAKEFLAGS; make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"; make DESTDIR="$pkgdir/" install

  install -Dm644 krb5.conf "$pkgdir/etc/krb5.conf"

  for i in rcp rsh telnet ftp su login; do
    rm -f "$pkgdir/usr/bin/$i"  "$pkgdir/usr/share/man/man1/$i.1"
  done
  for i in telnetd ftpd rshd; do
    rm -f "$pkgdir/usr/sbin/$i" "$pkgdir/usr/share/man/man8/$i.8"
  done
  rm -rf "$pkgdir/usr/share/man/cat"{1,3,5,8}

  rm "$pkgdir/usr/share/man/man5/ftpusers.5"*
  rm "$pkgdir/usr/share/man/man3/"{DES,DH,EVP,OpenSSL,RAND,RSA}*
  rm "$pkgdir/usr/share/man/man3/os.3"*

  install -Dm755 "$srcdir/kdc.rc.d"       "$pkgdir/etc/rc.d/kdc"
  install -Dm644 "$srcdir/kadmind.conf.d" "$pkgdir/etc/conf.d/kadmind"
  install -Dm644 "$srcdir/kpasswd.conf.d" "$pkgdir/etc/conf.d/kpasswd"
  install -Dm755 "$srcdir/kadmind.rc.d"   "$pkgdir/etc/rc.d/kadmind"
  install -Dm755 "$srcdir/kpasswd.rc.d"   "$pkgdir/etc/rc.d/kpasswd"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
