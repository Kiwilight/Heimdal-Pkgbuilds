# $Id: PKGBUILD 65540 2012-02-21 08:48:57Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Orivej Desh <smpuj@bk.ru>
# Maintainer: Orivej Desh <smpuj@bk.ru>

pkgname=gsasl
pkgver=1.6.1
pkgrel=4
pkgdesc="Simple Authentication and Security Layer framework and a few common SASL mechanisms"
arch=("i686" "x86_64")
url="http://josefsson.org/gsasl/"
license=("GPL")
depends=('gnutls' 'libidn' 'heimdal' 'libgcrypt')
_gentoo='http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/'
source=("ftp://ftp.gnu.org/gnu/gsasl/$pkgname-$pkgver.tar.gz"
  "$_gentoo/net-libs/libgsasl/files/libgsasl-gss-extra.patch")
md5sums=('7ae813eeb1f90ad52e9a781ac9e17f28'
         '489f6de9a1c5d15a201635357d8ddb5f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cd lib
  patch -p0 -i $srcdir/libgsasl-gss-extra.patch
  cd ..
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/share/info/dir" "$pkgdir/usr/lib/libgsasl.la"
}
